-- Naigama Assembly Grammar

TOP                <- INSTRUCTIONS
S                  <- %s+
__prefix           <- ( '--' [^\n]* '\n' / %s+ )*

INSTRUCTIONS       <- INSTRUCTION+ END
INSTRUCTION        <- {
                        ANYINSTR / BACKCOMMITINSTR / CALLINSTR /
                        CATCHINSTR / CHARINSTR / MASKEDCHARINSTR /
                        CLOSECAPTUREINSTR / COMMITINSTR /
                        STARTREPLACEINSTR / ENDREPLACEINSTR / REPLACEINSTR /
                        ENDINSTR / FAILTWICEINSTR / FAILINSTR /
                        JUMPINSTR / NOOPINSTR / TRAPINSTR /
                        OPENCAPTUREINSTR / PARTIALCOMMITINSTR /
                        QUADINSTR /
                        RETINSTR / SETINSTR / RANGEINSTR /
                        SKIPINSTR / SPANINSTR / TESTANYINSTR /
                        TESTCHARINSTR / TESTQUADINSTR /
                        TESTSETINSTR / VARINSTR /
                        COUNTERINSTR / CONDJUMPINSTR /
                        LABELDEF
                      }

END                <- !.

ANYINSTR           <- { 'any' }
BACKCOMMITINSTR    <- { 'backcommit' } S LABEL
CALLINSTR          <- { 'call' } S LABEL
CATCHINSTR         <- { 'catch' } S LABEL
CHARINSTR          <- { 'char' } S HEXBYTE
MASKEDCHARINSTR    <- { 'maskedchar' } S HEXBYTE S HEXBYTE
CLOSECAPTUREINSTR  <- { 'closecapture' } S SLOT (S TYPE)?
COMMITINSTR        <- { 'commit' } S LABEL
ENDINSTR           <- { 'end' } (S CODE )? 
FAILINSTR          <- { 'fail' }
FAILTWICEINSTR     <- { 'failtwice' }
JUMPINSTR          <- { 'jump' } S LABEL
NOOPINSTR          <- { 'noop' }
TRAPINSTR          <- { 'trap' }
OPENCAPTUREINSTR   <- { 'opencapture' } S SLOT
PARTIALCOMMITINSTR <- { 'partialcommit' } S LABEL
QUADINSTR          <- { 'quad' } S QUAD
REPLACEINSTR       <- { 'replace' } S LABEL S LABEL
STARTREPLACEINSTR  <- { 'startreplace' }
ENDREPLACEINSTR    <- { 'endreplace' }
RETINSTR           <- { 'ret' }
SETINSTR           <- { 'set' } S SET
RANGEINSTR         <- { 'range' } S NUMBER S NUMBER
SKIPINSTR          <- { 'skip' } S NUMBER
SPANINSTR          <- { 'span' } S SET
TESTANYINSTR       <- { 'testany' } S LABEL
TESTCHARINSTR      <- { 'testchar' } S HEXBYTE S LABEL ( S AMPERSAND HEXBYTE )?
TESTQUADINSTR      <- { 'testquad' } S QUAD S LABEL
TESTSETINSTR       <- { 'testset' } S SET S LABEL
VARINSTR           <- { 'var' } S SLOT
COUNTERINSTR       <- { 'counter' } S REGISTER S NUMBER
CONDJUMPINSTR      <- { 'condjump' } S REGISTER S LABEL

LABELDEF           <- { LABEL } COLON

CODE               <- NUMBER
HEXBYTE            <- { [0-9a-fA-F]^2 }
LABEL              <- { [a-zA-Z0-9_]^1-64 }
NUMBER             <- { [0-9]+ }
QUAD               <- { [0-9a-fA-F]^8 }
SET                <- { [0-9a-fA-F]^64 }
SLOT               <- NUMBER
REGISTER           <- NUMBER
TYPE               <- NUMBER
COLON              <- ':'
AMPERSAND          <- '&'
