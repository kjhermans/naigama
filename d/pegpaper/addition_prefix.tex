\section{Addition: Prefix Rule}

Consider the difference between the following two JSON grammar
definitions:

\begin{myquote}
\begin{verbatim}
JSON         <- %s* HASH %s* END
HASH         <- CBOPEN %s* OPTHASHELTS %s* CBCLOSE
OPTHASHELTS  <- HASHELTS / ...
HASHELTS     <- HASHELT %s* COMMA %s* HASHELTS / HASHELT
HASHELT      <- STRING %s* COLON %s* VALUE
ARRAY        <- ABOPEN %s* OPTARRAYELTS %s* ABCLOSE
OPTARRAYELTS <- ARRAYELTS / ...
ARRAYELTS    <- VALUE %s* COMMA %s* ARRAYELTS / VALUE
VALUE        <- STRING / INT / FLOAT / BOOL / NULL / HASH / ARRAY
STRING       <- { '"' ( '\\' ([nrtv"] / [0-9]^3) / [^"\\] )* '"' }
INT          <- { [0-9]+ }
FLOAT        <- { [0-9]* '.' [0-9]+ }
BOOL         <- { 'true' / 'false' }
NULL         <- { 'null' }
CBOPEN       <- '{'
CBCLOSE      <- '}'
ABOPEN       <- '['
ABCLOSE      <- ']'
COMMA        <- ','
COLON        <- ':'
END          <- !.

\end{verbatim}
\end{myquote}
\textit{Example of JSON grammar definition with explicit optional whitespace skipping}

\begin{myquote}
\begin{verbatim}
TOP          <- JSON
__prefix     <- %s*
JSON         <- HASH END
HASH         <- CBOPEN OPTHASHELTS CBCLOSE
OPTHASHELTS  <- HASHELTS / ...
HASHELTS     <- HASHELT COMMA HASHELTS / HASHELT
HASHELT      <- STRING COLON VALUE
ARRAY        <- ABOPEN OPTARRAYELTS ABCLOSE
OPTARRAYELTS <- ARRAYELTS / ...
ARRAYELTS    <- VALUE COMMA ARRAYELTS / VALUE
VALUE        <- STRING / FLOAT / INT / BOOL / NULL / HASH / ARRAY
STRING       <- { '"' ( '\\' ([nrtv"] / [0-9]^3) / [^"\\] )* '"' }
INT          <- { [0-9]+ }
FLOAT        <- { [0-9]* '.' [0-9]+ }
BOOL         <- { 'true' / 'false' }
NULL         <- { 'null' }
CBOPEN       <- '{'
CBCLOSE      <- '}'
ABOPEN       <- '['
ABCLOSE      <- ']'
COMMA        <- ','
COLON        <- ':'
END          <- !.

\end{verbatim}
\end{myquote}
\textit{Example of JSON grammar definition with implicit optional whitespace skipping}

The latter grammar definition is a bit longer (it makes a jump across the '\_\_prefix'
rule definition), but is much more readable with the clutter of optional whitespace
matchers removed. The rule defined as '\_\_prefix' takes care of that.

The prefix rule is a rule that is always called and executed
before the rest of the production logic of any of the rules
that are defined after it in a grammar.
