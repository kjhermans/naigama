-- Naigama Grammar Grammar

GRAMMAR            <- ( DEFINITION+ / SINGLE_EXPRESSION ) END

S                  <- %s+

COMMENT            <- '--' [^\n]* '\n'

__prefix           <- ( COMMENT / S )*

END                <- !.

DEFINITION         <- FUNCDECL / RULE

SINGLE_EXPRESSION  <- EXPRESSION

RULE               <- { IDENT } LEFTARROW EXPRESSION SEMICOLON

EXPRESSION         <- { TERMS } OR NOTHING /
                      { TERMS } OR EXPRESSION /
                      { TERMS }

TERMS              <- { TERM }+

TERM               <- { ENDOWEDMATCHER }

ENDOWEDMATCHER     <- { NOT / AND }? MATCHER { QUANTIFIER }?

QUANTIFIER         <- '?' /
                      '+' /
                      '*' /
                      '^' { [0-9]+ } '-' { [0-9]+ } /
                      '^' '-' { [0-9]+ } /
                      '^' { [0-9]+ } '-' /
                      '^' { [0-9]+ } /
                      '^$' { IDENT }

MATCHER            <- { ANY } /
                      { SET } /
                      { STRING } /
                      { BITMASK } /
                      { HEXLITERAL } /
                      { VARCAPTURE } /
                      { CAPTURE } /
                      { GROUP } /
                      { MACRO } /
                      { VARREFERENCE } /
                      { REFERENCE }

BITMASK            <- '|' [0-9a-fA-F]^2 '|' [0-9a-fA-F]^2 '|'

VARCAPTURE         <- CBOPEN COLON { IDENT } { CAPTURETYPE }? COLON
                      EXPRESSION { CBCLOSE } CAPTUREEND

CAPTURETYPE        <- SEMICOLON TYPE

TYPE               <- { 'uint32' / 'int32' }

CAPTURE            <- CBOPEN EXPRESSION { CBCLOSE } CAPTUREEND

GROUP              <- BOPEN EXPRESSION { BCLOSE }

CAPTUREEND         <- ( REPLACE / RECYCLE )?

SET                <- ABOPEN { SETNOT? } (
                        { '\\' ([nrtv\\\]\-] / [0-9]^3) / [^\\\]] } '-' 
                        { '\\' ([nrtv\\\]\-] / [0-9]^3) / [^\\\]] }
                        /
                        { '\\' ([nrtv\\\]\-] / [0-9]^3) / [^\\\]] }
                      )+ { ABCLOSE }

VARREFERENCE       <- '$' ( { IDENT } / { NUMBER } )

REFERENCE          <- IDENT !( LEFTARROW )

REPLACE            <- RIGHTARROW { REPLACETERMS }

REPLACETERMS       <- REPLACETERM+

REPLACETERM        <- { STRINGLITERAL } / { HEXLITERAL } / { VARREFERENCE }

RECYCLE            <- FATARROW { IDENT }

LEFTARROW          <- '<-'
RIGHTARROW         <- '->'
FATARROW           <- '=>'
NOT                <- '!'
AND                <- '&'
MACRO              <- '%' [a-zA-Z] [a-zA-Z0-9]*
HEXLITERAL         <- '0x' [0-9a-fA-F]^2
NUMBER             <- [0-9]+
STRING             <- STRINGLITERAL 'i'?
OR                 <- '/'
ANY                <- '.'
NOTHING            <- '...'
SETNOT             <- '^'

-- scripting engine

FUNCDECL       <- KW_FUNCTION S { IDENT } { FUNCPARAMDECL } { FUNCBODY }

FUNCPARAMDECL  <- BOPEN ( { PARAMDECL } ( COMMA { PARAMDECL } )* )? BCLOSE

PARAMDECL      <- { IDENT } { IDENT } / { IDENT }

FUNCBODY       <- CBOPEN { LOWSTMT }* CBCLOSE

LOWSTMT        <- { ST_IF } /
                  { ST_WHILE } /
                  { ST_RETURN } /
                  { ST_OTHER } /
                  { VARDECL } /
                  { ASSIGNMENT } /
                  ( SCR_EXPRESSION )? SEMICOLON

ST_IF          <- KW_IF BOPEN { SCR_EXPRESSION } BCLOSE
                    CBOPEN { LOWSTMT* } CBCLOSE
                    { IF_ELSEIF* }
                    { IF_ELSE? }

IF_ELSEIF      <- KW_ELSEIF BOPEN { SCR_EXPRESSION } BCLOSE
                    CBOPEN { LOWSTMT* } CBCLOSE 

IF_ELSE        <- KW_ELSE CBOPEN { LOWSTMT* } CBCLOSE

ST_WHILE       <- KW_WHILE BOPEN { SCR_EXPRESSION } BCLOSE
                    CBOPEN { LOWSTMT* } CBCLOSE

ST_RETURN      <- KW_RETURN { SCR_EXPRESSION? } SEMICOLON

ST_OTHER       <- ( KW_BREAK / KW_CONTINUE ) SEMICOLON

VARDECL        <- KW_VAR S IDENT ( ASSIGN { SCR_EXPRESSION } )? SEMICOLON

ASSIGNMENT     <- IDENT ASSIGNS SCR_EXPRESSION SEMICOLON

ASSIGNS        <- { ASSIGN / PLUSIS / MINIS / MULIS / DIVIS /
                    BITANDIS / BITORIS / BITXORIS / BITNOTIS /
                    LSHIFTIS / RSHIFTIS }

-- Copied from https://en.cppreference.com/w/c/language/operator_precedence
--
-- Precedence 	Operator 	Description 	Associativity
-- 1 	++ -- 		Suffix/postfix increment and decrement 	Left-to-right
-- 	() 		Function call
-- 	[] 		Array subscripting
-- 	. 		Structure and union member access
-- 	-> 		Structure and union member access through pointer
-- 	(type){list} 	Compound literal(C99)
-- 2 	++ -- 		Prefix increment and decrement[note 1] 	Right-to-left
-- 	+ - 		Unary plus and minus
-- 	! ~ 		Logical NOT and bitwise NOT
-- 	(type) 		Cast
-- 	* 		Indirection (dereference)
-- 	& 		Address-of
-- 	sizeof 		Size-of[note 2]
-- 	_Alignof 	Alignment requirement(C11)

-- 3 	* / % 		Multiplication, division, and remainder Left-to-right
-- 4 	+ - 		Addition and subtraction
-- 5 	<< >> 		Bitwise left shift and right shift
-- 6 	< <= 		For relational operators < and ≤ respectively
-- 	> >= 		For relational operators > and ≥ respectively
-- 7 	== != 		For relational = and ≠ respectively
-- 8 	& 		Bitwise AND
-- 9 	^ 		Bitwise XOR (exclusive or)
-- 10 	| 		Bitwise OR (inclusive or)
-- 11 	&& 		Logical AND
-- 12 	|| 		Logical OR

-- 13 	?: 		Ternary conditional[note 3] 	Right-to-Left
-- 14 	= 		Simple assignment
-- 	+= -= 		Assignment by sum and difference
-- 	*= /= %= 	Assignment by product, quotient, and remainder
-- 	<<= >>= 	Assignment by bitwise left shift and right shift
-- 	&= ^= |= 	Assignment by bitwise AND, XOR, and OR
-- 15 	, 		Comma 	Left-to-right 

SCR_EXPRESSION <- BINOP_P12

-- BINOP_P12      <- { BINOP_P11 } ( LOGOR BINOP_P11 )*
-- BINOP_P11      <- { BINOP_P10 } ( LOGAND BINOP_P10 )*
-- BINOP_P10      <- { BINOP_P09 } ( BITOR BINOP_P09 )*
-- BINOP_P09      <- { BINOP_P08 } ( BITXOR BINOP_P08 )*
-- BINOP_P08      <- { BINOP_P07 } ( BITAND BINOP_P07 )*
-- BINOP_P07      <- { BINOP_P06 } ( ( EQUALS / NEQUALS ) BINOP_P06 )*
-- BINOP_P06      <- { BINOP_P05 } ( ( LTEQ / LT / GTEQ / GT ) BINOP_P05 )*
-- BINOP_P05      <- { BINOP_P04 } ( ( LSHIFT / RSHIFT ) BINOP_P04 )*
-- BINOP_P04      <- { BINOP_P03 } ( ( ADD / SUB ) BINOP_P03 )*
-- BINOP_P03      <- { UNARIES }   ( ( MUL / DIV / POW ) UNARIES )*

BINOP_P12      <- { BINOP_P11 } { LOGOR SCR_EXPRESSION }*
BINOP_P11      <- { BINOP_P10 } { LOGAND SCR_EXPRESSION }*
BINOP_P10      <- { BINOP_P09 } { BITOR SCR_EXPRESSION }*
BINOP_P09      <- { BINOP_P08 } { BITXOR SCR_EXPRESSION }*
BINOP_P08      <- { BINOP_P07 } { BITAND SCR_EXPRESSION }*
BINOP_P07      <- { BINOP_P06 } { ( EQUALS / NEQUALS ) SCR_EXPRESSION }*
BINOP_P06      <- { BINOP_P05 } { ( LTEQ / LT / GTEQ / GT ) SCR_EXPRESSION }*
BINOP_P05      <- { BINOP_P04 } { ( LSHIFT / RSHIFT ) SCR_EXPRESSION }*
BINOP_P04      <- { BINOP_P03 } { ( ADD / SUB ) SCR_EXPRESSION }*
BINOP_P03      <- { UNARIES }   { ( MUL / DIV / POW ) SCR_EXPRESSION }*

UNARIES        <- { UNARY }* { SCR_TERM }

UNARY          <- { LOGNOT / BITNOT / INC / DEC }

SCR_TERM       <- { LITERAL } /
                  { FUNCTIONCALL } /
                  { SCR_REFERENCE } /
                  { BOPEN { SCR_EXPRESSION } BCLOSE }

FUNCTIONCALL   <- { SCR_REFERENCE }
                  BOPEN ( SCR_EXPRESSION ( COMMA SCR_EXPRESSION )* )? BCLOSE

SCR_REFERENCE  <- IDENT ( DOT IDENT )* INDEX*

INDEX          <- ABOPEN { SCR_EXPRESSION } ABCLOSE

LITERAL        <- STRINGLITERAL /
                  HASHLITERAL /
                  LISTLITERAL /
                  FLOATLITERAL /
                  INTLITERAL /
                  BOOLEANLITERAL

STRINGLITERAL  <- '\'' { ( '\\' ([nrtv\\'] / [0-9]^3) / [^'\\] )* } '\''
HASHLITERAL    <- { CBOPEN ( HASHELT ( COMMA HASHELT )* )? CBCLOSE }
HASHELT        <- { HASHKEY COLON HASHVALUE }
HASHKEY        <- { SCR_TERM }
HASHVALUE      <- { SCR_TERM }
LISTLITERAL    <- { ABOPEN ( LISTELT ( COMMA LISTELT )* )? ABCLOSE }
LISTELT        <- { SCR_TERM }
FLOATLITERAL   <- { [0-9]* '.' [0-9]+ }
INTLITERAL     <- { [0-9]+ }
BOOLEANLITERAL <- { 'true' / 'false' }

-- KW_IMPORT      <- 'import'
KW_FUNCTION    <- 'function'
KW_VAR         <- 'var'
KW_WHILE       <- 'while'
KW_RETURN      <- 'return'
KW_IF          <- 'if'
KW_ELSEIF      <- 'elseif'
KW_ELSE        <- 'else'
KW_BREAK       <- 'break'
KW_CONTINUE    <- 'continue'

IDENT          <- [a-zA-Z_][a-zA-Z0-9_]^0-63
BOPEN          <- '('
BCLOSE         <- ')'
CBOPEN         <- '{'
CBCLOSE        <- '}'
ABOPEN         <- '['
ABCLOSE        <- ']'
ASSIGN         <- { '=' }
PLUSIS         <- { '+=' }
MINIS          <- { '-=' }
MULIS          <- { '*=' }
DIVIS          <- { '/=' }
EQUALS         <- { '==' }
NEQUALS        <- { '!=' }
LT             <- { '<' } !'='
GT             <- { '>' } !'='
LTEQ           <- { '<=' }
GTEQ           <- { '>=' }
SEMICOLON      <- ';'
COLON          <- { ':' }
POW            <- { '**' }
MUL            <- { '*' } !'*'
DIV            <- { '/' } !'='
ADD            <- { '+' } ![+=]
SUB            <- { '-' } ![\-=]
INC            <- { '++' }
DEC            <- { '--' }
LOGAND         <- { '&&' }
LOGOR          <- { '||' }
LOGNOT         <- { '!' } !'='
BITAND         <- { '&' } ![&=]
BITOR          <- { '|' } ![|=]
BITXOR         <- { '^' } !'='
BITNOT         <- { '~' }
BITANDIS       <- { '&=' }
BITORIS        <- { '|=' }
BITXORIS       <- { '^=' }
BITNOTIS       <- { '~=' }
COMMA          <- ','
DOT            <- { '.' }
LSHIFT         <- { '<<' } !'='
RSHIFT         <- { '>>' } !'='
LSHIFTIS       <- { '<<=' }
RSHIFTIS       <- { '>>=' }
